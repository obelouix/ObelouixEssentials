import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os
import org.jetbrains.gradle.ext.Remote

plugins {
    id 'java'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0"
    id "de.undercouch.download" version "4.1.1"

}

group = 'fr.obelouix'
version = '1.0-SNAPSHOT'

sourceCompatibility = "16"
targetCompatibility = "16"
//see https://www.spigotmc.org/wiki/intellij-debug-your-plugin/
libsDirName = '../server/plugins'
ext {
    paperVersion = '1.16.5-670'
    paperURL = "https://papermc.io/api/v2/projects/paper/versions/1.16.5/builds/670/downloads/paper-${paperVersion}.jar"
}


repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven{
        name ='purpur-repo'
        url = 'https://repo.pl3x.net/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
    }
    maven {
        name ='ProtocolLib'
        url = 'https://repo.dmulloy2.net/repository/public/'
    }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'net.pl3x.purpur:purpur-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'io.papermc:paperlib:1.0.6'
    implementation 'net.luckperms:api:5.3'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.6.1-SNAPSHOT'

}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter(ReplaceTokens, tokens: ['version': project.version])
    }
}

/**
 * this task will setup the server folder
 */
task setupServer(type: Download) {
    new File("$projectDir", "server").mkdir()
    src "${paperURL}"
    dest "/server"
    overwrite false
    new File("$projectDir/server", "eula.txt").text = "eula=true"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        new File("$projectDir/server", "launch.bat").text = "java -Xmx2G -Xms2G -agentlib:jdwp=transport=dt_socket,server=n,address=localhost:5005,suspend=y -jar .\\paper-${paperVersion}.jar nogui"
    } else {
        new File("$projectDir/server", "launch.sh").text = "java -Xmx2G -Xms2G -agentlib:jdwp=transport=dt_socket,server=n,address=localhost:5005,suspend=y -jar ./paper-${paperVersion}.jar nogui"
    }
}

/**
 * This, will create the debug task
 */
idea {
    project {
        settings {
            taskTriggers {
                afterSync tasks.getByName("projects"), tasks.getByName("setupServer")
            }
            runConfigurations {
                "PaperMC Server"(Remote) {
                    mode = Remote.RemoteMode.LISTEN
                    host = "localhost"
                    port = 5005
                }
            }
        }
    }
}

